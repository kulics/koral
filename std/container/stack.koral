// ============================================================================
// std.container.Stack
// ============================================================================

public type [T Any]Stack(protected mut list [T]List)

public type [T Any]StackIterator(protected mut list [T]List, protected mut remaining UInt)

given[T Any] [T]Stack {
    public new() Self = [T]Stack([T]List.new())

    public with_capacity(capacity UInt) Self = [T]Stack([T]List.with_capacity(capacity))

    public count(self) UInt = self.list.count()

    public is_empty(self) Bool = self.list.is_empty()

    public clear(self ref) Void = {
        self.list.clear()
    }

    public push(self ref, value T) Void = {
        self.list.push(value)
    }

    public pop(self ref) [T]Option = self.list.pop()

    public peek(self) [T]Option = self.list.last()

    public iterator(self) [T]StackIterator = [T]StackIterator(self.list, self.list.count())

    public stream(self) [T, [T]StackIterator]Stream =
        [T, [T]StackIterator]Stream.new(self.iterator())
}

given[T Any] [T]StackIterator [T]Iterator {
    public next(self ref) [T]Option = {
        if self.remaining == 0 then {
            return [T]Option.None()
        }
        self.remaining = self.remaining - 1
        return self.list.get(self.remaining)
    }
}

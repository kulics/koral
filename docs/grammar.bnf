<program> ::= <item>*

<item> ::= <export-decl>
         | <import-decl>
         | <let-decl>
         | <type-decl>
         | <trait-decl>
         | <given-decl>
         | <annotation> <item>

<export-decl> ::= "export" <identifier> ";"

<import-decl> ::= "import" "{" <import-path> (";" <import-path>)* "}" ";"?
<import-path> ::= <identifier> ("." <identifier>)*

<let-decl> ::= "let" <binding-block> <block-terminator>
              | "let" <binding-simple> ";"

<binding-block> ::= <generic-params>? <identifier> <function-signature> "=" <block-expression>
                  | <mut>? <identifier> <type-annotation>? "=" <block-expression>

<binding-simple> ::= <generic-params>? <identifier> <function-signature> "=" <simple-expression>
                   | <mut>? <identifier> <type-annotation>? "=" <simple-expression>

<binding> ::= <binding-block> | <binding-simple>

<function-signature> ::=  "(" <param-list>? ")" <return-type>?
<param-list> ::= <param> ("," <param>)* ","?
<param> ::= <annotation>* <identifier> <type-annotation>?
<return-type> ::= <type-annotation>

<type-annotation> ::= <type>

<type-decl> ::= "type" <generic-params>? <identifier> <type-body>
<type-body> ::= "(" <field-list>? ")" ";"
              | "{" <variant-list> "}" <block-terminator>
              | "=" <type> ";"
<field-list> ::= <field> ("," <field>)*
<field> ::= <annotation>* <visibility>? <mut>? <identifier> <type-annotation>
<variant-list> ::= <variant> (";" <variant>)* ";"?
<variant> ::= <annotation>* <identifier> "(" <field-list>? ")"
<visibility> ::= "private"
                | "protected"
                | "public"

<trait-decl> ::= "trait" <generic-params>? <identifier> <trait-parents>? "{" <trait-member>* "}" <block-terminator>
<trait-parents> ::= <type-annotation> ("and" <type-annotation>)*
<trait-member> ::= <function-decl-signature> ";"

<given-decl> ::= "given" <given-head> <trait-impls>? "{" <given-member>* "}" <block-terminator>
<given-head> ::= <generic-params>? <type-name>
<trait-impls> ::= <type-annotation> ("and" <type-annotation>)*
<given-member> ::= <function-decl>

<function-decl> ::= <function-decl-signature> "=" <expression> ";"?
<function-decl-signature> ::= <generic-params>? <identifier> <function-signature>

<generic-params> ::= "[" <generic-param-list> "]"
<generic-param-list> ::= <generic-param> ("," <generic-param>)*
<generic-param> ::= <identifier> (<type-bound>)?
<type-bound> ::= <type-annotation>

<annotation> ::= "@" <identifier> ("(" <annotation-args>? ")")?
<annotation-args> ::= <annotation-arg> ("," <annotation-arg>)*
<annotation-arg> ::= <expression> | <identifier> "=" <expression>

<statement> ::= <let-decl>
              | <assignment-statement>
              | <expression-statement>

<assignment-statement> ::= <lhs> <assign-op> <block-expression> <block-terminator>
                         | <lhs> <assign-op> <simple-expression> ";"

<assign-op> ::= "=" | "+=" | "-=" | "*=" | "/=" | "%=" | "^=" | "bitand=" | "bitor=" | "bitxor=" | "bitshl=" | "bitshr="
<lhs> ::= <identifier> | <postfix-expression>

<expression-statement> ::= <block-expression> <block-terminator>
                         | <simple-expression> ";"

<block-terminator> ::= ";" | <newline>

<expression> ::= <block-expression>
               | <simple-expression>

<block-expression> ::= <block>
                     | <let-expression-block>
                     | <if-expression-block>
                     | <while-expression-block>
                     | <for-expression-block>
                     | <match-expression>
                     | <lambda-expression-block>

<simple-expression> ::= <let-expression-simple>
                      | <if-expression-simple>
                      | <while-expression-simple>
                      | <for-expression-simple>
                      | <lambda-expression-simple>
                      | <binary-expression>

<block> ::= "{" <statement>* (<expression>)? "}"

<let-expression-block> ::= "let" <binding> "then" <block-expression>
<let-expression-simple> ::= "let" <binding> "then" <simple-expression>

<if-expression-block> ::= "if" <expression> "then" <block-expression>
                        | "if" <expression> "then" <expression> "else" <block-expression>
<if-expression-simple> ::= "if" <expression> "then" <simple-expression>
                         | "if" <expression> "then" <expression> "else" <simple-expression>

<while-expression-block> ::= "while" <expression> "then" <block-expression>
                           | "while" <expression> "then" <expression> "else" <block-expression>
<while-expression-simple> ::= "while" <expression> "then" <simple-expression>
                            | "while" <expression> "then" <expression> "else" <simple-expression>

<for-expression-block> ::= "for" <pattern> "=" <expression> "then" <block-expression>
<for-expression-simple> ::= "for" <pattern> "=" <expression> "then" <simple-expression>

<match-expression> ::= <expression> "match" "{" <match-arm-list> "}"
<match-arm-list> ::= <match-arm> (";" <match-arm>)*
<match-arm> ::= <pattern> "->" <expression>

<lambda-expression-block> ::= <lambda-params> "->" <block-expression>
<lambda-expression-simple> ::= <lambda-params> "->" <simple-expression>
<lambda-params> ::= <identifier>
                  | "(" <param-list>? ")"

<binary-expression> ::= <logical-or-expression>
<logical-or-expression> ::= <logical-and-expression> ("or" <logical-and-expression>)*
<logical-and-expression> ::= <is-expression> ("and" <is-expression>)*
<is-expression> ::= <bitwise-or-expression> ("is" <pattern>)*
<bitwise-or-expression> ::= <bitwise-xor-expression> ("bitor" <bitwise-xor-expression>)*
<bitwise-xor-expression> ::= <bitwise-and-expression> ("bitxor" <bitwise-and-expression>)*
<bitwise-and-expression> ::= <equality-expression> ("bitand" <equality-expression>)*
<equality-expression> ::= <relational-expression> (("==" | "<>") <relational-expression>)*
<relational-expression> ::= <shift-expression> ((">" | "<" | ">=" | "<=") <shift-expression>)*
<shift-expression> ::= <additive-expression> (("bitshl" | "bitshr") <additive-expression>)*
<additive-expression> ::= <multiplicative-expression> (("+" | "-") <multiplicative-expression>)*
<multiplicative-expression> ::= <power-expression> (("*" | "/" | "%") <power-expression>)*
<power-expression> ::= <prefix-expression> ("^" <power-expression>)?
<prefix-expression> ::= ("not" | "bitnot" | "+" | "-") <prefix-expression> | <postfix-expression>
<postfix-expression> ::= <primary-expression> <postfix-suffix>*
<postfix-suffix> ::= <call-suffix> | <index-suffix> | <member-suffix>
<call-suffix> ::= "(" <argument-list>? ")"
<index-suffix> ::= "[" <expression> "]"
<member-suffix> ::= "." <generic-args>? <identifier>

<primary-expression> ::= <literal>
                       | <identifier>
                       | <type-constructor>
                       | <enum-constructor>
                       | <tuple-expression>
                       | <array-expression>
                       | <range-expression>
                       | <block>
                       | "(" <type> ")" <expression>
                       | "(" <expression> ")"

<type-constructor> ::= <type-name> "(" <argument-list>? ")"
<enum-constructor> ::= <type-name>? "." <identifier> "(" <argument-list>? ")"
<generic-args> ::= "[" <type-list> "]"
<tuple-expression> ::= "(" <expression-list>? ")"
<array-expression> ::= "[" <expression-list>? "]"
<expression-list> ::= <expression> ("," <expression>)*

<range-expression> ::= <expression> (".." | "..<" | "<.." | "<..<") <expression>
                    | <expression> ("..." | "<...")
                    | ("..." | "...<") <expression>
                    | "...."

<pattern> ::= "_"
            | <range-pattern>
            | <literal>
            | "mut"? <identifier>
            | <identifier> <type-annotation>
            | <type>
            | <tuple-pattern>
            | <constructor-pattern>
            | <enum-pattern>

<range-pattern> ::= <literal> (".." | "..<" | "<.." | "<..<") <literal>
                  | <literal> ("..." | "<...")
                  | ("..." | "...<") <literal>
                  | "...."

<tuple-pattern> ::= "(" <pattern-list>? ")"
<constructor-pattern> ::= <identifier> "(" <pattern-list>? ")"
<pattern-list> ::= <pattern> ("," <pattern>)*
<enum-pattern> ::= "." <identifier> ("(" <pattern-list>? ")")?

<type> ::= <type-name> ("ref")*

<type-name> ::= <identifier> ("." <identifier>)*
              | (<identifier> ".")* "[" <type-list> "]" <identifier>
              | (<identifier> ".")* "[" <integer-literal> "," <type-list> "]" <identifier>

<type-list> ::= <type> ("," <type>)*

<argument-list> ::= <expression> ("," <expression>)*

<literal> ::= <integer-literal>
            | <float-literal>
            | <string-literal>
            | <bool-literal>

<integer-literal> ::= [0-9]+
<float-literal> ::= [0-9]+ "." [0-9]+
<string-literal> ::= "\"" (~"\"")* "\"" | "'" (~"'")* "'"
<bool-literal> ::= "true" | "false"
<identifier> ::= [a-zA-Z_][a-zA-Z0-9_]*
<mut> ::= "mut"

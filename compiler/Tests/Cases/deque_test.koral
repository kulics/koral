// EXPECT: All Deque tests passed

using std.container.*

let main() Void = {
    let mut d = [Int]Deque.new()
    assert(d.is_empty(), "new deque is empty")

    d.push_back(2)
    d.push_back(3)
    d.push_front(1)

    assert(d.count() == 3, "count after pushes")
    assert(d.first().unwrap() == 1, "first value")
    assert(d.last().unwrap() == 3, "last value")
    assert(d.get(1).unwrap() == 2, "get middle")

    d[1] = 20
    assert(d[1] == 20, "subscript set/read")

    let p1 = d.pop_front()
    assert(p1.unwrap() == 1, "pop_front")
    let p2 = d.pop_back()
    assert(p2.unwrap() == 3, "pop_back")
    assert(d.count() == 1 and d.first().unwrap() == 20, "remaining value")

    let mut sum = 0
    let mut it = d.iterator()
    while true then {
        when it.next() in {
            .Some(v) then { sum = sum + v },
            .None then { break },
        }
    }
    assert(sum == 20, "iterator sum")

    let mut original = [Int]Deque.new()
    original.push_back(10)
    original.push_back(20)

    let mut copy = original
    copy.push_front(5)
    copy[1] = 99

    assert(original.count() == 2, "original count unchanged")
    assert(original[0] == 10 and original[1] == 20, "original values unchanged")
    assert(copy.count() == 3, "copy count")
    assert(copy[0] == 5 and copy[1] == 99 and copy[2] == 20, "copy modified")

    copy.clear()
    assert(copy.is_empty(), "clear works")
    assert(copy.pop_front().is_none(), "pop_front empty")
    assert(copy.pop_back().is_none(), "pop_back empty")

    println("All Deque tests passed")
}

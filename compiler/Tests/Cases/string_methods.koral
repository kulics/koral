// EXPECT: String advanced tests passed

let expect_some_uint(opt [UInt]Option, v UInt, msg String) Void = {
    when opt in {
        .Some(x) then assert(x == v, msg),
        .None then panic(msg),
    }
}

let test_capacity_and_empty() Void = {
    let s = "abc"
    assert(s.capacity() >= s.count(), "capacity")
    let e = String.zero()
    assert(e.count() == 0, "empty count")
    assert(e.is_empty(), "empty flag")
}

let test_new_constructor() Void = {
    let s = String.new()
    assert(s.count() == 0, "new count")
    assert(s.capacity() > 0, "new has capacity")
    assert(s.is_empty(), "new is empty")
}

let test_prefix_suffix() Void = {
    let s = "abcab"
    assert(s.starts_with("ab"), "starts_with")
    assert(s.ends_with("ab"), "ends_with")
    // find_index and find_last_index now take String
    expect_some_uint(s.find("b"), 1, "first index b")
    expect_some_uint(s.find_last("b"), 4, "last index b")
}

let test_trim_and_case() Void = {
    let s = "  HeLLo  "
    let trimmed = s.trim_ascii()
    assert(trimmed == "HeLLo", "trim both")
    assert(trimmed.trim_ascii_start() == "HeLLo", "trim start")
    assert(trimmed.trim_ascii_end() == "HeLLo", "trim end")
    assert(trimmed.to_ascii_lowercase() == "hello", "lowercase")
    assert(trimmed.to_ascii_uppercase() == "HELLO", "uppercase")
    assert("hELlo  woRLD".to_ascii_titlecase() == "Hello  World", "title")
}

let test_find_contains_replace_repeat() Void = {
    let s = "ababa"
    expect_some_uint(s.find("aba"), 0, "find aba")
    assert(s.contains("ba"), "contains ba")
    let r = "abcabc".replace_all("ab", "X")
    assert(r == "XcXc", "replace")
    let rep = "ab".repeat(3)
    assert(rep == "ababab", "repeat")
}

let test_ascii_checks() Void = {
    let s = "azAZ09"
    assert(s.is_ascii(), "ascii")
    let mut ws = " \t"// space and tab
    assert(ws.is_ascii_whitespace(), "blank yes")
    let a UInt8 = 97// 'a'
    ws.push(a)
    assert(not ws.is_ascii_whitespace(), "blank no")
}

let test_slice_and_remove_prefix_suffix() Void = {
    let s = "abcdef"
    assert(s.substring(2..4) == "cde", "slice closed")
    assert(s.substring(2..<5) == "cde", "slice closed-open")
    assert(s.remove_prefix("ab") == "cdef", "remove prefix")
    assert(s.remove_suffix("ef") == "abcd", "remove suffix")
}

let test_split_whitespace() Void = {
    let s = "  a  bc d  "
    let mut acc = String.zero()
    let mut first = true
    for part in s.split_ascii_whitespace() then {
        if not first then { acc.push(',') } // comma
        first = false
        acc.push_string(part)
    }
    assert(acc == "a,bc,d", "split ascii whitespace")
}

let test_split_sep_and_lines() Void = {
    let s = "a--b--"
    let mut parts = String.zero()
    let mut first = true
    for p in s.split("--") then {
        if not first then { parts.push(':') } // ':'
        first = false
        parts.push_string(p)
    }
    assert(parts == "a:b:", "split sep includes trailing empty")

    let l = "x\ny\n"
    let mut out = String.zero()
    let mut firstLine = true
    for line in l.lines() then {
        if not firstLine then { out.push(';') } // ';'
        firstLine = false
        out.push_string(line)
    }
    assert(out == "x;y", "lines split")
}

let test_join_strings() Void = {
    let mut names = [String]List.new()
    names.push("a")
    names.push("b")
    names.push("c")
    assert(join_strings(names, ", ") == "a, b, c", "join_strings basic")

    let mut single = [String]List.new()
    single.push("only")
    assert(join_strings(single, ", ") == "only", "join_strings single")

    let empty = [String]List.new()
    assert(join_strings(empty, ", ") == "", "join_strings empty")

    assert(join_strings(names, "") == "abc", "join_strings no separator")
}

let main() Void = {
    test_capacity_and_empty()
    test_new_constructor()
    test_prefix_suffix()
    test_trim_and_case()
    test_find_contains_replace_repeat()
    test_ascii_checks()
    test_slice_and_remove_prefix_suffix()
    test_split_whitespace()
    test_split_sep_and_lines()
    test_join_strings()
    println("String advanced tests passed")
}

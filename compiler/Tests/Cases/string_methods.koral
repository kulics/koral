// EXPECT: String advanced tests passed

let expect_some_int(opt [Int]Option, v Int, msg String) Void = {
    when opt is {
        .Some(x) then assert(x == v, msg),
        .None then panic(msg),
    }
}

let test_capacity_and_empty() Void = {
    let s = "abc"
    assert(s.capacity() >= s.count(), "capacity")
    let e = String.empty()
    assert(e.count() == 0, "empty count")
    assert(e.is_empty(), "empty flag")
}

let test_new_constructor() Void = {
    let s = String.new()
    assert(s.count() == 0, "new count")
    assert(s.capacity() > 0, "new has capacity")
    assert(s.is_empty(), "new is empty")
}

let test_prefix_suffix() Void = {
    let s = "abcab"
    assert(s.starts_with("ab"), "starts_with")
    assert(s.ends_with("ab"), "ends_with")
    // index_of and last_index_of now take String
    expect_some_int(s.index_of("b"), 1, "first index b")
    expect_some_int(s.last_index_of("b"), 4, "last index b")
}

let test_trim_and_case() Void = {
    let s = "  HeLLo  "
    let trimmed = s.trim_ascii()
    assert(trimmed == "HeLLo", "trim both")
    assert(trimmed.trim_ascii_start() == "HeLLo", "trim start")
    assert(trimmed.trim_ascii_end() == "HeLLo", "trim end")
    assert(trimmed.to_ascii_lowercase() == "hello", "lowercase")
    assert(trimmed.to_ascii_uppercase() == "HELLO", "uppercase")
    assert("hELlo  woRLD".to_ascii_titlecase() == "Hello  World", "title")
}

let test_find_contains_replace_repeat() Void = {
    let s = "ababa"
    expect_some_int(s.index_of("aba"), 0, "find aba")
    expect_some_int(s.index_of_after("aba", 1), 2, "index_of_after aba")
    assert(s.contains("ba"), "contains ba")
    let r = "abcabc".replace_all("ab", "X")
    assert(r == "XcXc", "replace")
    let rep = "ab".repeat(3)
    assert(rep == "ababab", "repeat")
}

let test_ascii_checks() Void = {
    let s = "azAZ09"
    assert(s.is_ascii(), "ascii")
    let mut ws = " \t"// space and tab
    assert(ws.is_ascii_whitespace(), "blank yes")
    let a UInt8 = 97// 'a'
    ws.push(a)
    assert(not ws.is_ascii_whitespace(), "blank no")
}

let test_slice_and_remove_prefix_suffix() Void = {
    let s = "abcdef"
    assert(s.slice(2..4) == "cde", "slice closed")
    assert(s.slice(2..<5) == "cde", "slice closed-open")
    assert(s.remove_prefix("ab") == "cdef", "remove prefix")
    assert(s.remove_suffix("ef") == "abcd", "remove suffix")
}

let test_split_whitespace() Void = {
    let s = "  a  bc d  "
    let mut it = s.split_ascii_whitespace().iterator()
    let mut acc = String.empty()
    let mut first = true
    let mut done = false
    while not done then {
        let n = it.next()
        when n is {
            .Some(part) then {
                if not first then { acc.push(',') } // comma
                first = false
                acc.push_string(part)
            },
            .None then { done = true },
        }
    }
    assert(acc == "a,bc,d", "split ascii whitespace")
}

let test_split_sep_and_lines() Void = {
    let s = "a--b--"
    let mut it = s.split("--").iterator()
    let mut parts = String.empty()
    let mut first = true
    let mut done = false
    while not done then {
        let n = it.next()
        when n is {
            .Some(p) then {
                if not first then { parts.push(':') } // ':'
                first = false
                parts.push_string(p)
            },
            .None then { done = true },
        }
    }
    assert(parts == "a:b:", "split sep includes trailing empty")

    let l = "x\ny\n"
    let mut lit = l.lines().iterator()
    let mut out = String.empty()
    let mut firstLine = true
    let mut done2 = false
    while not done2 then {
        let n = lit.next()
        when n is {
            .Some(line) then {
                if not firstLine then { out.push(';') } // ';'
                firstLine = false
                out.push_string(line)
            },
            .None then { done2 = true },
        }
    }
    assert(out == "x;y", "lines split")
}

let main() Void = {
    test_capacity_and_empty()
    test_new_constructor()
    test_prefix_suffix()
    test_trim_and_case()
    test_find_contains_replace_repeat()
    test_ascii_checks()
    test_slice_and_remove_prefix_suffix()
    test_split_whitespace()
    test_split_sep_and_lines()
    print_line("String advanced tests passed")
}

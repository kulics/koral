// EXPECT: All Buffer tests passed

using std.io.*

let read_all_string(mf ByteBuffer) String = {
    mf.seek(SeekPos.Start(0)).unwrap()
    return String.from_bytes(read_all_bytes(mf).unwrap())
}

let main() Void = {
    // === constructors ===
    let e = ByteBuffer.zero()
    e.seek(SeekPos.Start(0)).unwrap()
    assert(read_all_bytes(e).unwrap().count() == 0, "zero is empty")

    let c = ByteBuffer.with_capacity(16)
    c.seek(SeekPos.Start(0)).unwrap()
    assert(read_all_bytes(c).unwrap().count() == 0, "with_capacity is empty")

    let s = ByteBuffer.from_string("hello")
    assert(read_all_string(s) == "hello", "from_string content")

    let mut bytes = [UInt8]List.new()
    bytes.push(1)
    bytes.push(2)
    bytes.push(3)
    let fb = ByteBuffer.from_bytes(bytes)
    fb.seek(SeekPos.Start(0)).unwrap()
    let rb = read_all_bytes(fb).unwrap()
    assert(rb.count() == 3, "from_bytes length")
    assert(rb[0] == 1, "from_bytes[0]")

    // === write/read + seek ===
    let mf = ByteBuffer.zero()
    mf.write("abc".to_bytes(), ....).unwrap()
    mf.seek(SeekPos.Start(0)).unwrap()
    let mut out = make_bytes(8)
    let n = mf.read(ref out, ....).unwrap()
    assert(n == 3, "read count")
    assert(String.from_utf8_ptr(out.borrow_ptr(), n) == "abc", "read content")

    // append then read all
    mf.write("def".to_bytes(), ....).unwrap()
    assert(read_all_string(mf) == "abcdef", "append content")

    // range write
    let mut src = [UInt8]List.new()
    src.push(65)
    src.push(66)
    src.push(67)
    src.push(68)
    let mf2 = ByteBuffer.zero()
    let wn = mf2.write(src, 1..<3).unwrap()
    assert(wn == 2, "range write count")
    mf2.seek(SeekPos.Start(0)).unwrap()
    let mut out2 = make_bytes(4)
    let rn = mf2.read(ref out2, ....).unwrap()
    assert(rn == 2, "range read count")
    assert(out2[0] == 66 and out2[1] == 67, "range write data")

    // shared reference semantics
    let a = ByteBuffer.from_string("hi")
    let b = a
    a.write("!".to_bytes(), ....).unwrap()
    assert(read_all_string(b) == "hi!", "shared mutable content")

    // seek bounds
    let sk = b.seek(SeekPos.Start(100))
    when sk in {
        .Ok(_) then assert(false, "seek out of bounds should fail"),
        .Error(_) then {},
    }

    println("All Buffer tests passed")
}

// EXPECT: String tests passed

let test_literal_basics() Void = {
    let s = "hi"
    let h UInt8 = 104
    let i UInt8 = 105
    assert(s.count() == 2, "literal count")
    assert(not s.is_empty(), "literal not empty")
    assert(s[0] == h, "literal byte h")
    assert(s[1] == i, "literal byte i")
    // Test safe get method
    when s.get(0) is {
        .Some(b) then assert(b == h, "get byte h"),
        .None then panic("get should return Some"),
    }
    when s.get(100) is {
        .Some(_) then panic("get out of bounds should return None"),
        .None then {},
    }
}

let test_push() Void = {
    let mut s = "a"
    let b UInt8 = 98// 'b'
    s.push(b)
    assert(s.count() == 2, "push count")
    assert(s[1] == b, "push byte")
}

let test_push_string() Void = {
    let mut s = "ab"
    let t = "XYZ"
    let z UInt8 = 90// 'Z'
    s.push_string(t)
    assert(s.count() == 5, "push string count")
    assert(s[4] == z, "push string byte")
}

let test_push_int_string() Void = {
    let mut s = "value: "
    s.push_string(42.to_string())
    assert(s == "value: 42", "push int via to_string")
}

let test_push_uint_string() Void = {
    let mut s = "count: "
    s.push_string(((UInt)100).to_string())
    assert(s == "count: 100", "push uint via to_string")
}

let test_push_bool_string() Void = {
    let mut s = "flag: "
    s.push_string(true.to_string())
    assert(s == "flag: true", "push bool true via to_string")
    
    let mut s2 = "flag: "
    s2.push_string(false.to_string())
    assert(s2 == "flag: false", "push bool false via to_string")
}

let test_copy_on_write() Void = {
    let mut a = "hi"
    let b = a// Copy should share storage
    let bang UInt8 = 33// '!'
    let h UInt8 = 104// 'h'

    a.push(bang)

    assert(a.count() == 3, "a mutated count")
    assert(a[2] == bang, "a mutated byte")

    // Original copy remains unchanged
    assert(b.count() == 2, "b preserved count")
    assert(b[0] == h, "b preserved byte")
}

let test_add_operator() Void = {
    let a = "hello"
    let b = " world"
    let c = a + b
    assert(c == "hello world", "string + string")

    let left = "" + a
    assert(left == a, "empty + a")

    let right = a + ""
    assert(right == a, "a + empty")
}

let main() Void = {
    test_literal_basics()
    test_push()
    test_push_string()
    test_push_int_string()
    test_push_uint_string()
    test_push_bool_string()
    test_copy_on_write()
    test_add_operator()
    println("String tests passed")
}

// ============================================================================
// Koral Standard Library - Traits
// ============================================================================
// NOTE: This file is merged into core.koral, so primitives are already available.
// ============================================================================

// ============================================================================
// Core Traits
// ============================================================================

public trait Equatable {
    __equals(self, other Self) Bool
}

public trait Comparable Equatable {
    __compare(self, other Self) Int
}

// Hash utilities
public let hash_combine(seed UInt, value UInt) UInt = {
    let magic UInt = 2654435769  // 0x9e3779b9
    seed ^ {value + magic + {seed << 6} + {seed >> 2}}
}

public trait Hashable Equatable {
    hash(self) UInt
}

public trait ToString {
    to_string(self) String
}

// Generic Iterator trait for for-loop support
public trait [T Any]Iterator {
    next(self ref) [T]Option
}

// Iterable trait - types that can produce an iterator
public trait [T Any, R [T]Iterator]Iterable {
    iterator(self) R
}

// ============================================================================
// Primitive Hashable Implementations
// ============================================================================

given Bool {
    public hash(self) UInt = if self then 1u else 0u
}

given UInt {
    public hash(self) UInt = self
}

given UInt8 {
    public hash(self) UInt = (UInt)self
}

given UInt16 {
    public hash(self) UInt = (UInt)self
}

given UInt32 {
    public hash(self) UInt = (UInt)self
}

given UInt64 {
    public hash(self) UInt = {
        let v UInt64 = self
        let lo UInt = (UInt)v
        let hi UInt = (UInt){ v / (UInt64)4294967296 }
        hash_combine(hash_combine(0u, lo), hi)
    }
}

given Int {
    public hash(self) UInt = (UInt)self
}

given Int8 {
    public hash(self) UInt = (UInt)self
}

given Int16 {
    public hash(self) UInt = (UInt)self
}

given Int32 {
    public hash(self) UInt = (UInt)self
}

given Int64 {
    public hash(self) UInt = {
        let v UInt64 = (UInt64)self
        let lo UInt = (UInt)v
        let hi UInt = (UInt){ v / (UInt64)4294967296 }
        hash_combine(hash_combine(0u, lo), hi)
    }
}

given Float32 {
    public hash(self) UInt = {
        let bits UInt32 = self.to_bits()
        hash_combine(0u, (UInt)bits)
    }
}

given Float64 {
    public hash(self) UInt = {
        let bits UInt64 = self.to_bits()
        let lo UInt = (UInt)bits
        let hi UInt = (UInt){ bits / 4294967296u64 }
        hash_combine(hash_combine(0u, lo), hi)
    }
}

given[T Any] [T]Pointer {
    public hash(self) UInt = (UInt)self
}

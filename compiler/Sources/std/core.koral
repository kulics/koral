
public intrinsic type Bool;
public intrinsic type Never;

public intrinsic type Int;
public intrinsic type Int8;
public intrinsic type Int16;
public intrinsic type Int32;
public intrinsic type Int64;

public intrinsic type UInt;
public intrinsic type UInt8;
public intrinsic type UInt16;
public intrinsic type UInt32;
public intrinsic type UInt64;

public intrinsic type Float32;
public intrinsic type Float64;

public intrinsic type [T Any]Pointer;

// Memory management primitives
public intrinsic let [T Any]ref_count(r T ref) Int;

// Allocates uninitialized memory for `capacity` number of elements.
// The caller is responsible for deallocating this memory later.
public intrinsic let [T Any]alloc_memory(capacity Int) [T]Pointer;

// Deallocates the memory pointed to by `ptr`.
// PRECONDITION: The memory must be deinitialized (i.e. uninitialized).
// If the memory contains values that need to be dropped, call `ptr.deinit()` first.
public intrinsic let [T Any]dealloc_memory(ptr [T]Pointer) Void;

// Copies `count` elements from `source` to `dest`.
// The memory regions must NOT overlap.
// PRECONDITION: Memory at `dest` must be uninitialized. `source` must be initialized.
// POSTCONDITION: Memory at `dest` is initialized. `source` remains initialized.
public intrinsic let [T Any]copy_memory(dest [T]Pointer, source [T]Pointer, count Int) Void;

// Moves `count` elements from `source` to `dest`.
// The memory regions may overlap.
// PRECONDITION: Memory at `dest` must be uninitialized. `source` must be initialized.
// POSTCONDITION: Memory at `dest` is initialized. Memory at `source` becomes uninitialized.
public intrinsic let [T Any]move_memory(dest [T]Pointer, source [T]Pointer, count Int) Void;

// String storage and COW String
public type StringStorage(mut data [UInt8]Pointer, mut len Int, mut cap Int);

given StringStorage {
    // [Compiler Protocol] drop releases underlying buffer
    __drop(self ref) = {
        dealloc_memory(self.data);
    };
};

public type String(mut storage StringStorage ref) Copy;

// Forward declarations for iterator helpers used in `given String`
public type StringSplitAsciiWhitespace(source String, mut index Int);
public type StringSplit(source String, sep String, mut index Int);
public type StringLines(source String, mut index Int);

// type Byte = UInt8;
// type Rune = UInt32;

// builtin
// trait Any {}

// Most pointer operations are available for Any type [T].
intrinsic given[T Any] [T]Pointer {
    // --- Access & Mutation ---

    // Initializes this pointerâ€™s memory with the given value.
    // This writes to *uninitialized* memory and does NOT drop any existing value.
    // POSTCONDITION: Memory at `self` is initialized.
    init(self, v T) Void;

    // Deinitializes the memory at `self`.
    // This drops the value and returns the memory to an uninitialized state.
    // PRECONDITION: Memory at `self` must be initialized.
    deinit(self) Void;

    // Accesses the value pointed to by `self` without removing or invalidating it.
    // Returns a reference of the value.
    // PRECONDITION: Memory at `self` must be initialized.
    // NOTE: This is the primitive operation used to implement `__at` for higher constructs.
    peek(self) T ref;

    // Retrieves the value from `self`, leaving the memory uninitialized.
    // Ideally used for moving values out of the pointer.
    // PRECONDITION: Memory at `self` must be initialized.
    // POSTCONDITION: Memory at `self` is uninitialized.
    take(self) T;

    // Replaces the value at `self` with `v`, returning the old value.
    // Neither the old value nor the new value is dropped.
    // PRECONDITION: Memory at `self` must be initialized.
    replace(self, v T) T;

    // --- Arithmetic ---

    // Returns a new pointer moved forward by `n` elements from `self`.
    // The offset is in units of T (stride), not bytes.
    offset(self, n Int) [T]Pointer;

    // Compiler protocol: Equatable
    __equals(self ref, other [T]Pointer ref) Bool;

    // Compiler protocol: Comparable
    __compare(self ref, other [T]Pointer ref) Int;
};

// ---------------- Primitive Equatable (intrinsic) ----------------

intrinsic given Bool {
    __equals(self ref, other Bool ref) Bool;
};

intrinsic given Int {
    __equals(self ref, other Int ref) Bool;
};
intrinsic given Int8 {
    __equals(self ref, other Int8 ref) Bool;
};
intrinsic given Int16 {
    __equals(self ref, other Int16 ref) Bool;
};
intrinsic given Int32 {
    __equals(self ref, other Int32 ref) Bool;
};
intrinsic given Int64 {
    __equals(self ref, other Int64 ref) Bool;
};

intrinsic given UInt {
    __equals(self ref, other UInt ref) Bool;
};
intrinsic given UInt8 {
    __equals(self ref, other UInt8 ref) Bool;
};
intrinsic given UInt16 {
    __equals(self ref, other UInt16 ref) Bool;
};
intrinsic given UInt32 {
    __equals(self ref, other UInt32 ref) Bool;
};
intrinsic given UInt64 {
    __equals(self ref, other UInt64 ref) Bool;
};

intrinsic given Float32 {
    __equals(self ref, other Float32 ref) Bool;
};
intrinsic given Float64 {
    __equals(self ref, other Float64 ref) Bool;
};

// ---------------- Primitive Comparable (intrinsic) ----------------

intrinsic given Bool {
    __compare(self ref, other Bool ref) Int;
};

intrinsic given Int {
    __compare(self ref, other Int ref) Int;
};
intrinsic given Int8 {
    __compare(self ref, other Int8 ref) Int;
};
intrinsic given Int16 {
    __compare(self ref, other Int16 ref) Int;
};
intrinsic given Int32 {
    __compare(self ref, other Int32 ref) Int;
};
intrinsic given Int64 {
    __compare(self ref, other Int64 ref) Int;
};

intrinsic given UInt {
    __compare(self ref, other UInt ref) Int;
};
intrinsic given UInt8 {
    __compare(self ref, other UInt8 ref) Int;
};
intrinsic given UInt16 {
    __compare(self ref, other UInt16 ref) Int;
};
intrinsic given UInt32 {
    __compare(self ref, other UInt32 ref) Int;
};
intrinsic given UInt64 {
    __compare(self ref, other UInt64 ref) Int;
};

intrinsic given Float32 {
    __compare(self ref, other Float32 ref) Int;
};
intrinsic given Float64 {
    __compare(self ref, other Float64 ref) Int;
};

// Temporary IO
public intrinsic let print_string(message String) Void;
public intrinsic let print_int(value Int) Void;
public intrinsic let print_bool(value Bool) Void;
public intrinsic let panic(message String) Never;
public intrinsic let exit(code Int) Never;
public intrinsic let abort() Never;

// Bit-level utilities (intrinsic)
// Used for stable hashing of floating point values.
public intrinsic let float32_bits(value Float32) UInt32;
public intrinsic let float64_bits(value Float64) UInt64;

// ---------------- Option ----------------
public type [T Copy] Option {
    None();
    Some(value T);
};

given [T Copy] [T]Option {
    public is_some(self ref) Bool = {
        when deref self is {
            .Some(_) then true;
            .None then false;
        }
    }

    public is_none(self ref) Bool = {
        not self.is_some()
    }

    public unwrap(self) T = {
        when self is {
            .Some(v) then v;
            .None then panic("Option.unwrap failed: value is None");
        }
    }

    public unwrap_or(self, default T) T = {
        when self is {
            .Some(v) then v;
            .None then default;
        }
    }
};

// ---------------- Result ----------------
public type [T Copy, E Copy] Result {
    Ok(value T);
    Err(error E);
};

given [T Copy, E Copy] [T, E]Result {
    public is_ok(self ref) Bool = {
        when deref self is {
            .Ok(_) then true;
            .Err(_) then false;
        }
    }

    public is_err(self ref) Bool = {
        not self.is_ok()
    }

    public unwrap(self) T = {
        when self is {
            .Ok(v) then v;
            .Err(_) then panic("Result.unwrap failed: value is Err");
        }
    }

    public unwrap_err(self) E = {
        when self is {
            .Ok(_) then panic("Result.unwrap_err failed: value is Ok");
            .Err(e) then e;
        }
    }
};

// ---------------- String ----------------

given String {
    // Unsafe constructor: does not validate UTF-8
    public from_utf8_bytes_unchecked(bytes [UInt8]Pointer, len Int) String = {
        let cap = len + 1; // keep room for trailing 0
        let data = [UInt8]alloc_memory(cap);
        copy_memory(data, bytes, len);
        data.offset(len).init(0);
        let storage = ref StringStorage(data, len, cap);
        String(storage)
    };

    public empty() String = {
        let data = [UInt8]alloc_memory(1);
        data.init(0);
        let storage = ref StringStorage(data, 0, 1);
        String(storage)
    };

    private is_ascii_space(b UInt8) Bool =
        b == " " or b == "\t" or b == "\n" or b == "\v" or b == "\f" or b == "\r";

    private bounds_check_read(self, index Int) Void = {
        if index < 0 or index > self.storage.len then { panic("String index out of bounds"); };
    };

    private bounds_check_write(self, index Int) Void = {
        if index < 0 or index >= self.storage.len then { panic("String index out of bounds"); };
    };

    private ensure_unique(self ref) Void = {
        if ref_count(self.storage) > 1 then {
            let old = self.storage;
            let new_data = [UInt8]alloc_memory(old.cap);
            copy_memory(new_data, old.data, old.len + 1); // copy including trailing 0
            let new_storage = ref StringStorage(new_data, old.len, old.cap);
            self.storage = new_storage;
        }
    };

    private ensure_capacity(self ref, min_cap Int) Void = {
        if self.storage.cap >= min_cap then { return; };
        let mut new_cap = self.storage.cap * 2;
        if new_cap < min_cap then { new_cap = min_cap; };

        let new_data = [UInt8]alloc_memory(new_cap);
        copy_memory(new_data, self.storage.data, self.storage.len + 1);
        let new_storage = ref StringStorage(new_data, self.storage.len, new_cap);
        self.storage = new_storage;
    };

    public count_byte(self) Int = self.storage.len;

    public is_empty(self) Bool = self.storage.len == 0;

    public get_byte(self, index Int) UInt8 = {
        self.bounds_check_read(index);
        deref self.storage.data.offset(index).peek()
    };

    public set_byte(self ref, index Int, value UInt8) Void = {
        self.bounds_check_write(index);
        self.ensure_unique();
        self.storage.data.offset(index).replace(value);
    };

    public push_byte(self ref, value UInt8) Void = {
        self.ensure_unique();
        let needed = self.storage.len + 2; // new byte + trailing 0
        self.ensure_capacity(needed);
        self.storage.data.offset(self.storage.len).init(value);
        self.storage.len = self.storage.len + 1;
        self.storage.data.offset(self.storage.len).replace(0);
    };

    public push(self ref, other String) Void = {
        let other_len = other.storage.len;
        self.ensure_unique();
        let needed = self.storage.len + other_len + 1;
        self.ensure_capacity(needed);
        let dest = self.storage.data.offset(self.storage.len);
        copy_memory(dest, other.storage.data, other_len);
        self.storage.len = self.storage.len + other_len;
        self.storage.data.offset(self.storage.len).replace(0);
    };

    public reserve_byte(self ref, capacity Int) Void = {
        self.ensure_unique();
        let need = capacity + 1;
        self.ensure_capacity(need);
    };

    public capacity_byte(self) Int = self.storage.cap - 1;

    public starts_with(self, prefix String) Bool = {
        if prefix.storage.len > self.storage.len then { return false; };
        let mut i = 0;
        while i < prefix.storage.len then {
            if self.get_byte(i) <> prefix.get_byte(i) then { return false; };
            i += 1;
        };
        true
    };

    public ends_with(self, suffix String) Bool = {
        let slen = suffix.storage.len;
        if slen > self.storage.len then { return false; };
        let offset = self.storage.len - slen;
        let mut i = 0;
        while i < slen then {
            if self.get_byte(offset + i) <> suffix.get_byte(i) then { return false; };
            i += 1;
        };
        true
    };

    public contains_byte(self, b UInt8) Bool = {
        when self.first_index_of_byte(b) is {
            .Some(_) then true;
            .None then false
        }
    };

    public first_index_of_byte(self, b UInt8) [Int]Option = {
        let mut i = 0;
        while i < self.storage.len then {
            if self.get_byte(i) == b then { return [Int]Option.Some(i); };
            i += 1;
        };
        [Int]Option.None()
    };

    public last_index_of_byte(self, b UInt8) [Int]Option = {
        let mut i = self.storage.len;
        while i > 0 then {
            i -= 1;
            if self.get_byte(i) == b then { return [Int]Option.Some(i); };
        };
        [Int]Option.None()
    };

    private make_substring(self, start Int, len Int) String = {
        if start < 0 or len < 0 or start + len > self.storage.len then {
            panic("substring out of bounds");
        };
        let cap = len + 1;
        let data = [UInt8]alloc_memory(cap);
        copy_memory(data, self.storage.data.offset(start), len);
        data.offset(len).init(0);
        let storage = ref StringStorage(data, len, cap);
        String(storage)
    };

    public substring(self, start Int, len Int) String = self.make_substring(start, len);

    public trim_ascii_start(self) String = {
        let mut i = 0;
        while i < self.storage.len and String.is_ascii_space(self.get_byte(i)) then { i += 1; };
        self.make_substring(i, self.storage.len - i)
    };

    public trim_ascii_end(self) String = {
        let mut end = self.storage.len;
        while end > 0 and String.is_ascii_space(self.get_byte(end - 1)) then { end -= 1; };
        self.make_substring(0, end)
    };

    public trim_ascii_whitespace(self) String = {
        let head = self.trim_ascii_start();
        head.trim_ascii_end()
    };

    private to_lower_byte(b UInt8) UInt8 =
        if b >= 65 and b <= 90 then b + 32 else b;

    private to_upper_byte(b UInt8) UInt8 =
        if b >= 97 and b <= 122 then b - 32 else b;

    public to_ascii_lowercase(self) String = {
        let cap = self.storage.len + 1;
        let data = [UInt8]alloc_memory(cap);
        let mut i = 0;
        while i < self.storage.len then {
            data.offset(i).init(String.to_lower_byte(self.get_byte(i)));
            i += 1;
        };
        data.offset(self.storage.len).init(0);
        let storage = ref StringStorage(data, self.storage.len, cap);
        String(storage)
    };

    public to_ascii_uppercase(self) String = {
        let cap = self.storage.len + 1;
        let data = [UInt8]alloc_memory(cap);
        let mut i = 0;
        while i < self.storage.len then {
            data.offset(i).init(String.to_upper_byte(self.get_byte(i)));
            i += 1;
        };
        data.offset(self.storage.len).init(0);
        let storage = ref StringStorage(data, self.storage.len, cap);
        String(storage)
    };

    public to_ascii_title(self) String = {
        let cap = self.storage.len + 1;
        let data = [UInt8]alloc_memory(cap);
        let mut in_word = false;
        let mut i = 0;
        while i < self.storage.len then {
            let b = self.get_byte(i);
            if String.is_ascii_space(b) then {
                in_word = false;
                data.offset(i).init(b);
            } else {
                if not in_word then {
                    in_word = true;
                    data.offset(i).init(String.to_upper_byte(b));
                } else {
                    data.offset(i).init(String.to_lower_byte(b));
                }
            };
            i += 1;
        };
        data.offset(self.storage.len).init(0);
        let storage = ref StringStorage(data, self.storage.len, cap);
        String(storage)
    };

    public insert_byte(self ref, index Int, value UInt8) Void = {
        if index < 0 or index > self.storage.len then { panic("insert out of bounds"); };
        self.ensure_unique();
        let needed = self.storage.len + 2;
        self.ensure_capacity(needed);
        let src = self.storage.data.offset(index);
        let dest = self.storage.data.offset(index + 1);
        let count = self.storage.len - index + 1; // include trailing 0
        move_memory(dest, src, count);
        self.storage.data.offset(index).replace(value);
        self.storage.len = self.storage.len + 1;
    };

    public insert(self ref, index Int, value String) Void = {
        if index < 0 or index > self.storage.len then { panic("insert out of bounds"); };
        let vlen = value.storage.len;
        if vlen == 0 then { return; };
        self.ensure_unique();
        let needed = self.storage.len + vlen + 1;
        self.ensure_capacity(needed);
        let src = self.storage.data.offset(index);
        let dest = self.storage.data.offset(index + vlen);
        let count = self.storage.len - index + 1; // include trailing 0
        move_memory(dest, src, count);
        copy_memory(self.storage.data.offset(index), value.storage.data, vlen);
        self.storage.len = self.storage.len + vlen;
    };

    public remove_byte(self ref, index Int) UInt8 = {
        if index < 0 or index >= self.storage.len then { panic("remove out of bounds"); };
        self.ensure_unique();
        let old = deref self.storage.data.offset(index).peek();
        let dest = self.storage.data.offset(index);
        let src = self.storage.data.offset(index + 1);
        let count = self.storage.len - index; // include trailing 0
        move_memory(dest, src, count);
        self.storage.len = self.storage.len - 1;
        old
    };

    public pop_byte(self ref) [UInt8]Option =
        if self.storage.len == 0 then { [UInt8]Option.None() }
        else { [UInt8]Option.Some(self.remove_byte(self.storage.len - 1)) };

    public is_ascii(self) Bool = {
        let mut i = 0;
        while i < self.storage.len then {
            if self.get_byte(i) >= 128 then { return false; };
            i += 1;
        };
        true
    };

    public is_ascii_blank(self) Bool = {
        let mut i = 0;
        while i < self.storage.len then {
            let b = self.get_byte(i);
            if not String.is_ascii_space(b) then { return false; };
            i += 1;
        };
        true
    };

    public __equals(self ref, other String ref) Bool = {
        if self.storage.len <> other.storage.len then { return false; };
        let mut i = 0;
        while i < self.storage.len then {
            if self.get_byte(i) <> other.get_byte(i) then { return false; };
            i += 1;
        };
        true
    };

    public __compare(self ref, other String ref) Int = {
        let min = if self.storage.len < other.storage.len then self.storage.len else other.storage.len;
        let mut i = 0;
        while i < min then {
            let a = self.get_byte(i);
            let b = other.get_byte(i);
            if a < b then { return 0 - 1; };
            if a > b then { return 1; };
            i += 1;
        };
        if self.storage.len < other.storage.len then 0 - 1
        else if self.storage.len > other.storage.len then 1
        else 0
    };

    public find_from(self, pat String, start Int) [Int]Option = {
        if pat.storage.len == 0 then { panic("find empty pattern"); };
        if start < 0 or start > self.storage.len then { panic("find start out of bounds"); };
        let mut i = start;
        let max = self.storage.len - pat.storage.len;
        while i <= max then {
            let mut j = 0;
            let mut ok = true;
            while j < pat.storage.len then {
                if self.get_byte(i + j) <> pat.get_byte(j) then { ok = false; break; };
                j += 1;
            };
            if ok then { return [Int]Option.Some(i); };
            i += 1;
        };
        [Int]Option.None()
    };

    public find(self, pat String) [Int]Option = self.find_from(pat, 0);

    public contains(self, pat String) Bool = {
        when self.find_from(pat, 0) is {
            .Some(_) then true;
            .None then false
        }
    };

    public count_string(self, pat String) Int = {
        let mut count = 0;
        let mut idxOpt = self.find_from(pat, 0);
        while true then {
            when idxOpt is {
                .Some(i) then {
                    count += 1;
                    idxOpt = self.find_from(pat, i + pat.storage.len);
                };
                .None then { break; }
            };
        };
        count
    };

    public repeat(self, times Int) String = {
        if times < 0 then { panic("repeat negative"); };
        if times == 0 then { return String.empty(); };
        let total = self.storage.len * times;
        let cap = total + 1;
        let data = [UInt8]alloc_memory(cap);
        let mut offset = 0;
        let mut t = 0;
        while t < times then {
            copy_memory(data.offset(offset), self.storage.data, self.storage.len);
            offset += self.storage.len;
            t += 1;
        };
        data.offset(total).init(0);
        let storage = ref StringStorage(data, total, cap);
        String(storage)
    };

    public replace(self, pat String, with String) String = {
        if pat.storage.len == 0 then { panic("replace empty pattern"); };
        let mut result = String.empty();
        let mut start = 0;
        let mut idxOpt = self.find_from(pat, start);
        while true then {
            when idxOpt is {
                .Some(idx) then {
                    let prefix_len = idx - start;
                    if prefix_len > 0 then {
                        let slice = self.make_substring(start, prefix_len);
                        result.push(slice);
                    };
                    result.push(with);
                    start = idx + pat.storage.len;
                    idxOpt = self.find_from(pat, start);
                };
                .None then { break; }
            };
        };
        if start < self.storage.len then {
            let tail = self.make_substring(start, self.storage.len - start);
            result.push(tail);
        };
        result
    };

    public split_ascii_whitespace(self) StringSplitAsciiWhitespace =
        StringSplitAsciiWhitespace(self, 0);

    public split(self, sep String) StringSplit = {
        if sep.storage.len == 0 then { panic("split empty separator"); };
        StringSplit(self, sep, 0)
    };

    public lines(self) StringLines = StringLines(self, 0);

    public remove_prefix(self, prefix String) String =
        if self.starts_with(prefix) then { self.make_substring(prefix.storage.len, self.storage.len - prefix.storage.len) } else { self }
    ;

    public remove_suffix(self, suffix String) String = {
        if self.ends_with(suffix) then {
            let remain = self.storage.len - suffix.storage.len;
            self.make_substring(0, remain)
        } else { self }
    };
};

given StringSplitAsciiWhitespace {
    public next(self ref) [String]Option = {
        let len = self.source.count_byte();
        let mut i = self.index;
        while i < len and String.is_ascii_space(self.source.get_byte(i)) then { i += 1; };
        if i >= len then { return [String]Option.None(); };
        let start = i;
        while i < len and not String.is_ascii_space(self.source.get_byte(i)) then { i += 1; };
        let part = self.source.substring(start, i - start);
        self.index = i;
        [String]Option.Some(part)
    };
};

given StringSplit {
    public next(self ref) [String]Option = {
        let len = self.source.count_byte();
        if self.index > len then { return [String]Option.None(); };
        let mut idxOpt = self.source.find_from(self.sep, self.index);
        when idxOpt is {
            .Some(i) then {
                let part = self.source.substring(self.index, i - self.index);
                self.index = i + self.sep.count_byte();
                [String]Option.Some(part)
            };
            .None then {
                let part = self.source.substring(self.index, len - self.index);
                self.index = len + 1;
                [String]Option.Some(part)
            }
        }
    };
};

given StringLines {
    public next(self ref) [String]Option = {
        let len = self.source.count_byte();
        if self.index > len then { return [String]Option.None(); };
        // Do not yield a trailing empty line when the string ends with '\n'.
        if self.index == len then {
            self.index = len + 1;
            return [String]Option.None();
        };
        let mut iOpt = self.source.find_from("\n", self.index);
        when iOpt is {
            .Some(i) then {
                let part = self.source.substring(self.index, i - self.index);
                self.index = i + 1;
                [String]Option.Some(part)
            };
            .None then {
                let part = self.source.substring(self.index, len - self.index);
                self.index = len + 1;
                [String]Option.Some(part)
            }
        }
    };
};

public type [T Copy]List(private mut source [T]Pointer, private mut len Int, private mut cap Int);

given[T Copy] [T]List {
    public new() Self = [T]List([T]alloc_memory(10), 0, 10);

    // [Compiler Protocol]
    // Automatically called when the object goes out of scope.
    // User code cannot call this directly.
    __drop(self ref) = {
        let mut i = 0;
        while i < self.len then {
            self.source.offset(i).deinit();
            i += 1;
        };
        dealloc_memory(self.source);
    }

    public copy(self ref) Self = {
        // IMPORTANT: Do NOT use `copy_memory` here.
        // `copy_memory` is a raw memcpy and would skip retain/copy semantics for Copy types
        // like `ref T` and `String`.
        let new_ptr = [T]alloc_memory(self.cap);
        let mut i = 0;
        while i < self.len then {
            let v = deref self.source.offset(i).peek();
            new_ptr.offset(i).init(v);
            i += 1;
        };
        [T]List(new_ptr, self.len, self.cap)
    }

    public count(self ref) Int = self.len;

    // [Compiler Protocol]
    // Lowering target for `list[index]` in read context.
    // Returns a copy of the element (List requires T: Copy).
    public __at(self ref, index Int) T = {
        if index < 0 or index >= self.len then { panic("List index out of bounds"); };
        deref self.source.offset(index).peek()
    }

    // [Compiler Protocol]
    // Lowering target for `list[index] = value`.
    public __update_at(self ref, index Int, value T) Void = {
        if index < 0 or index >= self.len then { panic("List index out of bounds"); };
        // Replace element (handles ref-counted Copy types via init/deinit)
        self.source.offset(index).deinit();
        self.source.offset(index).init(value);
    }

    public push_back(self ref, value T) Void = {
         self.ensure_capacity(self.len + 1);
        self.source.offset(self.len).init(value);
        self.len = self.len + 1;
    }

    public insert(self ref, index Int, value T) Void = {
        if index < 0 or index > self.len then { panic("List insert index out of bounds"); };
        self.ensure_capacity(self.len + 1);
        
        if index < self.len then {
            let src = self.source.offset(index);
            let dest = self.source.offset(index + 1);
            let count = self.len - index;
            move_memory(dest, src, count);
            self.source.offset(index).init(value);
        } else {
             self.source.offset(self.len).init(value);
        };
        self.len = self.len + 1;
    }

    public remove(self ref, index Int) Bool = {
        if index < 0 or index >= self.len then { return false; };
        // 1. Drop the element at index
        self.source.offset(index).deinit();
        
        // 2. Shift remaining elements down
        if index < self.len - 1 then {
            let src = self.source.offset(index + 1);
            let dest = self.source.offset(index);
            let count = self.len - 1 - index;
            
            // Move [index+1...len-1] to [index...len-2]
            move_memory(dest, src, count);
        };
        
        self.len = self.len - 1;
        true
    };

    public pop_back(self ref) [T]Option = {
        if self.len == 0 then { return [T]Option.None(); };
        let idx = self.len - 1;
        let p = self.source.offset(idx);
        let v = p.take();
        self.len = idx;
        [T]Option.Some(v)
    };

    // Ensures the list is unique and has enough capacity
    private ensure_capacity(self ref, min_capacity Int) Void = {
         // Unique case - Grow if needed
         if min_capacity > self.cap then {
             let mut new_cap = self.cap * 2;
             if new_cap < min_capacity then {
                 new_cap = min_capacity;
             };
             
             let new_source = [T]alloc_memory(new_cap);
             move_memory(new_source, self.source, self.len);
             dealloc_memory(self.source);
             
             self.source = new_source;
             self.cap = new_cap;
         };
    }

    public iterator(self ref) [T]ListIterator = {
        // Capture a ref to the list instead of exposing raw Pointer
        [T]ListIterator(self, 0)
    };
};

// Iterator now holds a ref to the list to avoid exposing raw pointers
public type [T Copy]ListIterator(source [T]List ref, mut index Int);

given[T Copy] [T]ListIterator {
    public next(self ref) [T]Option = 
        if self.index < self.source.count() then {
            let val = self.source[self.index];
            self.index += 1;
            [T]Option.Some(val)
        } else {
            [T]Option.None()
        };
};

/*
NOTE: The iterator/sequence trait sketches below require *generic traits* and/or
trait method generics, which are not supported in trait v1 yet. Keep as design
notes until that feature lands.

public trait [T Any]Iterator {
    next(self Self ref) [T]Option;
}

public trait [T Any, I [T]Iterator]IntoIterator {
    iterator(self) I;
}

public let [T Any, I [T]Iterator]lazy(iter I) [T, I]Sequence = Sequence(iter);

public type [T Any, I [T]Iterator]Sequence(iter I);

given[T Any, I [T]Iterator] [T, I]Sequence {
    public [R Any]reduce(self, mut r R, f [R, T, R]Func) R = {
        for v = self then r = f(r, v);
        r
    }
    public filter(self, f [T, Bool]Func) [T, I]FilterSequence = FilterSequence(self, f);
    public [R Any]map(self, f [T, R]Func) [T, R, I]MapSequence = MapSequence(self, f);
}

public type [T Any, I [T]Iterator]FilterSequence(iter I, filter [T, Bool]Func);

given[T Any, I [T]Iterator] [T, I]FilterSequence {
    public next(self Self ref) [T]Option = 
        while self.iter.next() is .Some(v) then {
            if self.filter(v) then return .Some(v);
        } else .None();
}

public type [T Any, R Any, I [T]Iterator]MapSequence(iter I, mapper [T, R]Func);

given[T Any, R Any, I [T]Iterator] [T, R, I]MapSequence  {
    next(self Self ref) [R]Option = 
        if self.iter.next() is .Some(v) then .Some(mapper(v)) else .None();
}
*/

// ---------------- Traits (v1) ----------------

public trait Equatable {
    __equals(self ref, other Self ref) Bool;
};

// Hash utilities (UInt-only, no bitwise required)
// Seed ^= value + 0x9e3779b9 + (seed << 6) + (seed >> 2)
public let hash_combine(seed UInt, value UInt) UInt = {
    // 0x9e3779b9 = 2654435769
    let magic UInt = 2654435769;
    seed bitxor {value + magic + {seed bitshl 6} + {seed bitshr 2}}
};

public trait Hashable Equatable {
    hash(self ref) UInt;
};

// Keys usable in hash tables.
// v1: require `Copy` so Map/Set can store values safely in raw slots.
public trait HashKey Hashable and Copy {};

public trait Comparable Equatable {
    __compare(self ref, other Self ref) Int;
};

// ---------------- Hashable (std implementations) ----------------

given Bool {
    public hash(self ref) UInt = {
        let mut h UInt = 0;
        if deref self then { h = 1; };
        h
    };
};

// Identity hash for UInt.
given UInt {
    public hash(self ref) UInt = deref self;
};

// Hashes for smaller unsigned ints.
given UInt8 { public hash(self ref) UInt = (UInt)deref self; };
given UInt16 { public hash(self ref) UInt = (UInt)deref self; };
given UInt32 { public hash(self ref) UInt = (UInt)deref self; };

// Hash UInt64 by mixing low/high parts (portable even if UInt is 32-bit).
given UInt64 {
    public hash(self ref) UInt = {
        let v UInt64 = deref self;
        let lo UInt = (UInt)v;
        let hi UInt = (UInt){ v / (UInt64)4294967296 };
        hash_combine(hash_combine(0, lo), hi)
    };
};

// Minimal, deterministic String hash (currently length-based).
// This satisfies the Hashable contract (equal => same hash) and is a placeholder
// until numeric casts/bitwise ops across integer widths are available.
given String {
    // Deterministic byte-based hash.
    // Uses `hash_combine` so it doesn't require bitwise ops.
    public hash(self ref) UInt = {
        let s = deref self;
        let mut h UInt = 2166136261;
        let mut i Int = 0;
        while i < s.count_byte() then {
            let b UInt8 = s.get_byte(i);
            h = hash_combine(h, (UInt)b);
            i += 1;
        };
        h
    };
};

// Hashes for signed ints (cast to UInt keeps equality=>same-hash for the same type).
given Int { public hash(self ref) UInt = (UInt)deref self; };
given Int8 { public hash(self ref) UInt = (UInt)deref self; };
given Int16 { public hash(self ref) UInt = (UInt)deref self; };
given Int32 { public hash(self ref) UInt = (UInt)deref self; };

// Hash Int64 by mixing low/high parts.
given Int64 {
    public hash(self ref) UInt = {
        let v UInt64 = (UInt64)deref self;
        let lo UInt = (UInt)v;
        let hi UInt = (UInt){ v / (UInt64)4294967296 };
        hash_combine(hash_combine(0, lo), hi)
    };
};

// Hash floats via bit-pattern.
given Float32 {
    public hash(self ref) UInt = {
        let bits UInt32 = float32_bits(deref self);
        hash_combine(0, (UInt)bits)
    };
};

given Float64 {
    public hash(self ref) UInt = {
        let bits UInt64 = float64_bits(deref self);
        let lo UInt = (UInt)bits;
        let hi UInt = (UInt){ bits / (UInt64)4294967296 };
        hash_combine(hash_combine(0, lo), hi)
    };
};

given[T Any] [T]Pointer {
    public hash(self ref) UInt = (UInt)deref self;
};

// ---------------- Map / Set ----------------

// Separate chaining using bucket array backed by raw Pointer.
// Each bucket stores an optional ref to a singly-linked Node.

public type [K HashKey, V Copy]MapNode(hash UInt, key K, mut value V, mut next_node [[K, V]MapNode ref]Option);
public type [K HashKey, V Copy]MapItem(key K, value V) Copy;

given[K HashKey, V Copy] [K, V]MapNode {
    public next(self ref) [[K, V]MapNode ref]Option = self.next_node;
    public set_next(self ref, next [[K, V]MapNode ref]Option) Void = { self.next_node = next; };
};

public type [K HashKey, V Copy]Map(
    private mut buckets [[[K, V]MapNode ref]Option]Pointer,
    private mut cap Int,
    private mut len Int
);

public type [K HashKey, V Copy]MapIterator(source [K, V]Map ref, mut bucket Int, mut current [[K, V]MapNode ref]Option);

given[K HashKey, V Copy] [K, V]Map {
    public new() Self = [K, V]Map.with_capacity(16);

    public with_capacity(capacity Int) Self = {
        let mut cap = capacity;
        if cap < 16 then { cap = 16; };

        let buckets = [[[K, V]MapNode ref]Option]alloc_memory(cap);
        let mut i = 0;
        while i < cap then {
            buckets.offset(i).init([[K, V]MapNode ref]Option.None());
            i += 1;
        };
        [K, V]Map(buckets, cap, 0)
    };

    // Drop buckets and all nodes.
    __drop(self ref) = {
        let mut i = 0;
        while i < self.cap then {
            self.buckets.offset(i).deinit();
            i += 1;
        };
        dealloc_memory(self.buckets);
    };

    public copy(self ref) Self = {
        let mut out = [K, V]Map.with_capacity(self.cap);
        let mut it = self.iterator();
        while true then {
            when it.next() is {
                .Some(item) then { out.insert(item.key, item.value); };
                .None then { break; }
            }
        };
        out
    };

    public count(self ref) Int = self.len;
    public capacity(self ref) Int = self.cap;
    public is_empty(self ref) Bool = self.len == 0;

    public clear(self ref) Void = {
        let mut i = 0;
        while i < self.cap then {
            let ptr = self.buckets.offset(i);
            let old = ptr.take();
            // dropping `old` releases the chain
            ptr.init([[K, V]MapNode ref]Option.None());
            i += 1;
        };
        self.len = 0;
    };

    public reserve(self ref, additional Int) Void = {
        if additional <= 0 then { return; };
        self.ensure_capacity(self.len + additional);
    };

    public contains_key(self ref, key K) Bool = {
        let h = key.hash();
        let idx = self.bucket_index(h);
        let mut cur = deref self.buckets.offset(idx).peek();
        while true then {
            when cur is {
                .Some(nref) then {
                    let node = deref nref;
                    if node.key == key then { return true; };
                    cur = node.next_node;
                };
                .None then { return false; }
            }
        }
    };

    public get(self ref, key K) [V]Option = {
        let h = key.hash();
        let idx = self.bucket_index(h);
        let mut cur = deref self.buckets.offset(idx).peek();
        while true then {
            when cur is {
                .Some(nref) then {
                    let node = deref nref;
                    if node.key == key then { return [V]Option.Some(node.value); };
                    cur = node.next_node;
                };
                .None then { return [V]Option.None(); }
            }
        }
    };

    // Overwrite semantics:
    // - returns true if a new key was inserted
    // - returns false if an existing key was overwritten
    public insert(self ref, key K, value V) Bool = {
        self.ensure_capacity(self.len + 1);

        let h = key.hash();
        let idx = self.bucket_index(h);
        let bucket_ptr = self.buckets.offset(idx);

        let mut cur = deref bucket_ptr.peek();
        while true then {
            when cur is {
                .Some(nref) then {
                    let node = deref nref;
                    if node.key == key then {
                        node.value = value;
                        return false;
                    };
                    cur = node.next_node;
                };
                .None then { break; }
            }
        };

        // Insert new node at head
        let head = bucket_ptr.take();
        let new_node = ref [K, V]MapNode(h, key, value, head);
        bucket_ptr.init([[K, V]MapNode ref]Option.Some(new_node));
        self.len = self.len + 1;
        true
    };

    public remove(self ref, key K) [V]Option = {
        let h = key.hash();
        let idx = self.bucket_index(h);
        let bucket_ptr = self.buckets.offset(idx);
        let mut head = deref bucket_ptr.peek();

        when head is {
            .None then { return [V]Option.None(); };
            .Some(nref) then {
                let node = deref nref;
                if node.key == key then {
                    let next = node.next_node;
                    bucket_ptr.replace(next);
                    self.len = self.len - 1;
                    return [V]Option.Some(node.value);
                };
                let mut prev = nref;
                let mut cur = node.next_node;
                while true then {
                    when cur is {
                        .Some(cref) then {
                            let cnode = deref cref;
                            if cnode.key == key then {
                                let next = cnode.next_node;
                                deref prev . next_node = next;
                                self.len = self.len - 1;
                                return [V]Option.Some(cnode.value);
                            };
                            prev = cref;
                            cur = cnode.next_node;
                        };
                        .None then { return [V]Option.None(); }
                    }
                }
            }
        }
    };

    public iterator(self ref) [K, V]MapIterator = [K, V]MapIterator(self, 0, [[K, V]MapNode ref]Option.None());

    private bucket_index(self ref, hash UInt) Int = {
        let capU UInt = (UInt)self.cap;
        let div = hash / capU;
        let m UInt = hash - div * capU;
        (Int)m
    };

    private ensure_capacity(self ref, min_entries Int) Void = {
        let mut limit = self.cap * 3 / 4; // load factor 0.75
        if limit == 0 then { limit = 1; };
        if min_entries <= limit then { return; };

        let mut new_cap = self.cap * 2;
        if new_cap < 16 then { new_cap = 16; };
        while min_entries > new_cap * 3 / 4 then {
            new_cap = new_cap * 2;
        };
        self.rehash_into(new_cap);
    };

    private rehash_into(self ref, new_cap Int) Void = {
        let mut new_map = [K, V]Map.with_capacity(new_cap);
        let mut it = self.iterator();
        while true then {
            when it.next() is {
                .Some(item) then { new_map.insert(item.key, item.value); };
                .None then { break; }
            }
        };

        let old_buckets = self.buckets;
        let old_cap = self.cap;
        let old_len = self.len;

        self.buckets = new_map.buckets;
        self.cap = new_map.cap;
        self.len = new_map.len;

        new_map.buckets = old_buckets;
        new_map.cap = old_cap;
        new_map.len = old_len;
    };

    private bucket_index_for_hash(self ref, hash UInt, cap Int) Int = {
        let capU UInt = (UInt)cap;
        let div = hash / capU;
        let m UInt = hash - div * capU;
        (Int)m
    };
};

given[K HashKey, V Copy] [K, V]MapIterator {
    public next(self ref) [[K, V]MapItem]Option = {
        while true then {
            when self.current is {
                .Some(nref) then {
                    let node = deref nref;
                    self.current = node.next_node;
                    return [[K, V]MapItem]Option.Some([K, V]MapItem(node.key, node.value));
                };
                .None then {
                    if self.bucket >= self.source.cap then { return [[K, V]MapItem]Option.None(); };
                    let head = deref self.source.buckets.offset(self.bucket).peek();
                    self.bucket = self.bucket + 1;
                    self.current = head;
                }
            }
        }
    };
};

public type [T HashKey]SetNode(hash UInt, value T, mut next_node [[T]SetNode ref]Option);

given[T HashKey] [T]SetNode {
    public next(self ref) [[T]SetNode ref]Option = self.next_node;
    public set_next(self ref, next [[T]SetNode ref]Option) Void = { self.next_node = next; };
};

public type [T HashKey]Set(
    private mut buckets [[[T]SetNode ref]Option]Pointer,
    private mut cap Int,
    private mut len Int
);

public type [T HashKey]SetIterator(source [T]Set ref, mut bucket Int, mut current [[T]SetNode ref]Option);

given[T HashKey] [T]Set {
    public new() Self = [T]Set.with_capacity(16);

    public with_capacity(capacity Int) Self = {
        let mut cap = capacity;
        if cap < 16 then { cap = 16; };

        let buckets = [[[T]SetNode ref]Option]alloc_memory(cap);
        let mut i = 0;
        while i < cap then {
            buckets.offset(i).init([[T]SetNode ref]Option.None());
            i += 1;
        };
        [T]Set(buckets, cap, 0)
    };

    __drop(self ref) = {
        let mut i = 0;
        while i < self.cap then {
            self.buckets.offset(i).deinit();
            i += 1;
        };
        dealloc_memory(self.buckets);
    };

    public copy(self ref) Self = {
        let mut out = [T]Set.with_capacity(self.cap);
        let mut it = self.iterator();
        while true then {
            when it.next() is {
                .Some(v) then { out.insert(v); };
                .None then { break; }
            }
        };
        out
    };

    public count(self ref) Int = self.len;
    public capacity(self ref) Int = self.cap;
    public is_empty(self ref) Bool = self.len == 0;

    public clear(self ref) Void = {
        let mut i = 0;
        while i < self.cap then {
            let ptr = self.buckets.offset(i);
            let old = ptr.take();
            ptr.init([[T]SetNode ref]Option.None());
            i += 1;
        };
        self.len = 0;
    };

    public reserve(self ref, additional Int) Void = {
        if additional <= 0 then { return; };
        self.ensure_capacity(self.len + additional);
    };

    public contains(self ref, value T) Bool = {
        let h = value.hash();
        let idx = self.bucket_index(h);
        let mut cur = deref self.buckets.offset(idx).peek();
        while true then {
            when cur is {
                .Some(nref) then {
                    let node = deref nref;
                    if node.value == value then { return true; };
                    cur = node.next_node;
                };
                .None then { return false; }
            }
        }
    };

    // Returns true if newly inserted, false if already present.
    public insert(self ref, value T) Bool = {
        self.ensure_capacity(self.len + 1);

        let h = value.hash();
        let idx = self.bucket_index(h);
        let bucket_ptr = self.buckets.offset(idx);

        let mut cur = deref bucket_ptr.peek();
        while true then {
            when cur is {
                .Some(nref) then {
                    let node = deref nref;
                    if node.value == value then { return false; };
                    cur = node.next_node;
                };
                .None then { break; }
            }
        };

        let head = bucket_ptr.take();
        let new_node = ref [T]SetNode(h, value, head);
        bucket_ptr.init([[T]SetNode ref]Option.Some(new_node));
        self.len = self.len + 1;
        true
    };

    public remove(self ref, value T) Bool = {
        let h = value.hash();
        let idx = self.bucket_index(h);
        let bucket_ptr = self.buckets.offset(idx);
        let mut head = deref bucket_ptr.peek();

        when head is {
            .None then { return false; };
            .Some(nref) then {
                let node = deref nref;
                if node.value == value then {
                    let next = node.next_node;
                    bucket_ptr.replace(next);
                    self.len = self.len - 1;
                    return true;
                };
                let mut prev = nref;
                let mut cur = node.next_node;
                while true then {
                    when cur is {
                        .Some(cref) then {
                            let cnode = deref cref;
                            if cnode.value == value then {
                                let next = cnode.next_node;
                                deref prev . next_node = next;
                                self.len = self.len - 1;
                                return true;
                            };
                            prev = cref;
                            cur = cnode.next_node;
                        };
                        .None then { return false; }
                    }
                }
            }
        }
    };

    public union(self ref, other [T]Set ref) [T]Set = {
        let mut out = [T]Set.with_capacity(self.len + other.len);
        let mut it = self.iterator();
        while true then {
            when it.next() is {
                .Some(v) then { out.insert(v); };
                .None then { break; }
            }
        };
        let mut it2 = other.iterator();
        while true then {
            when it2.next() is {
                .Some(v) then { out.insert(v); };
                .None then { break; }
            }
        };
        out
    };

    public intersection(self ref, other [T]Set ref) [T]Set = {
        let min_cnt = if self.len < other.len then self.len else other.len;
        let mut out = [T]Set.with_capacity(min_cnt);
        let mut it = self.iterator();
        while true then {
            when it.next() is {
                .Some(v) then { if other.contains(v) then { out.insert(v); }; };
                .None then { break; }
            }
        };
        out
    };

    public difference(self ref, other [T]Set ref) [T]Set = {
        let mut out = [T]Set.with_capacity(self.len);
        let mut it = self.iterator();
        while true then {
            when it.next() is {
                .Some(v) then { if not other.contains(v) then { out.insert(v); }; };
                .None then { break; }
            }
        };
        out
    };

    public iterator(self ref) [T]SetIterator = [T]SetIterator(self, 0, [[T]SetNode ref]Option.None());

    private bucket_index(self ref, hash UInt) Int = {
        let capU UInt = (UInt)self.cap;
        let div = hash / capU;
        let m UInt = hash - div * capU;
        (Int)m
    };

    private ensure_capacity(self ref, min_entries Int) Void = {
        let mut limit = self.cap * 3 / 4;
        if limit == 0 then { limit = 1; };
        if min_entries <= limit then { return; };

        let mut new_cap = self.cap * 2;
        if new_cap < 16 then { new_cap = 16; };
        while min_entries > new_cap * 3 / 4 then {
            new_cap = new_cap * 2;
        };
        self.rehash_into(new_cap);
    };

    private rehash_into(self ref, new_cap Int) Void = {
        let mut new_set = [T]Set.with_capacity(new_cap);
        let mut it = self.iterator();
        while true then {
            when it.next() is {
                .Some(v) then { new_set.insert(v); };
                .None then { break; }
            }
        };

        let old_buckets = self.buckets;
        let old_cap = self.cap;
        let old_len = self.len;

        self.buckets = new_set.buckets;
        self.cap = new_set.cap;
        self.len = new_set.len;

        new_set.buckets = old_buckets;
        new_set.cap = old_cap;
        new_set.len = old_len;
    };

    private bucket_index_for_hash(self ref, hash UInt, cap Int) Int = {
        let capU UInt = (UInt)cap;
        let div = hash / capU;
        let m UInt = hash - div * capU;
        (Int)m
    };
};

given[T HashKey] [T]SetIterator {
    public next(self ref) [T]Option = {
        while true then {
            when self.current is {
                .Some(nref) then {
                    let node = deref nref;
                    self.current = node.next_node;
                    return [T]Option.Some(node.value);
                };
                .None then {
                    if self.bucket >= self.source.cap then { return [T]Option.None(); };
                    let head = deref self.source.buckets.offset(self.bucket).peek();
                    self.bucket = self.bucket + 1;
                    self.current = head;
                }
            }
        }
    };
};

public let [T Comparable]max(a T, b T) T = if a > b then a else b;
public let [T Comparable]min(a T, b T) T = if a < b then a else b;
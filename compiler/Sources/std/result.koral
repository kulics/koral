// ============================================================================
// Koral Standard Library - Result Type
// ============================================================================
// NOTE: This file is merged into core.koral, so primitives, traits, and option are already available.
// ============================================================================

// ============================================================================
// Result Type Definition
// ============================================================================

public type [T Any, E Any] Result {
    Ok(value T),
    Error(error E),
}

// ============================================================================
// Result Methods
// ============================================================================

given [T Any, E Any] [T, E]Result {
    public is_ok(self) Bool = when self is {
        .Ok(_) then true,
        .Error(_) then false,
    }

    public is_error(self) Bool = when self is {
        .Error(_) then true,
        .Ok(_) then false,
    }

    public unwrap(self) T = when self is {
        .Ok(v) then v,
        .Error(_) then panic("Result.unwrap failed: value is Error"),
    }

    public unwrap_error(self) E = when self is {
        .Error(e) then e,
        .Ok(_) then panic("Result.unwrap_error failed: value is Ok"),
    }
    
    public unwrap_or(self, default T) T = when self is {
        .Ok(v) then v,
        .Error(_) then default,
    }
}
